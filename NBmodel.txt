model{
  # likelihood
  for(j in 1 : N) {
  for(k in 1 : T) {


  log(lambda[j, k]) <- a0 + beta.Base * (log.Base4[j] - log.Base4.bar)
  + beta.Trt * (Trt[j] - Trt.bar)
  + beta.BT * (BT[j] - BT.bar)
  + beta.Age * (log.Age[j] - log.Age.bar)
  + beta.V4 * (V4[k] - V4.bar)
  p[j, k] <- r[j,k]/(r[j,k]+lambda[j,k])
  y[j, k] ~ dnegbin( p[j,k], r[j,k])
  r[j, k] ~ dunif(0.0001,1000)
  }
  BT[j] <- Trt[j] * log.Base4[j] # interaction
  log.Base4[j] <- log(Base[j] / 4)
  log.Age[j] <- log(Age[j])
  }
  
  #covariate means:
  log.Age.bar <- mean(log.Age[])
  Trt.bar <- mean(Trt[])
  BT.bar <- mean(BT[])
  log.Base4.bar <- mean(log.Base4[])
  V4.bar <- mean(V4[])
  # priors:
 
  a0 ~ dnorm(0.0,1.0E-4)
  beta.Base ~ dnorm(0.0,1.0E-4)
  beta.Trt ~ dnorm(0.0,1.0E-4);
  beta.BT ~ dnorm(0.0,1.0E-4)
  beta.Age ~ dnorm(0.0,1.0E-4)
  beta.V4 ~ dnorm(0.0,1.0E-4)

  # re-calculate intercept on original scale:
  alpha0 <- a0 - beta.Base * log.Base4.bar - beta.Trt * Trt.bar
  - beta.BT * BT.bar - beta.Age * log.Age.bar - beta.V4 * V4.bar

}
